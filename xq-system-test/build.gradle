plugins {
    id 'java'
    id 'com.avast.gradle.docker-compose' version '0.17.6'
}

group = 'xq'
version = '1.0.0'

ext {
    karateVersion = '1.5.1'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation "io.karatelabs:karate-junit5:${karateVersion}"
    testImplementation(project(":xq-api-test-plan"))
    testImplementation(project(":jvm-kit-common"))
}

test {
    useJUnitPlatform()
    systemProperty "karate.env", System.properties.getProperty("karate.env", "system")
    systemProperty "karate.options", "--tags @regression"
    outputs.upToDateWhen { false }
}

sourceSets {
    test {
        resources {
            srcDir file('src/test/java')
            exclude '**/*.java'
        }
    }
}

// Optional: Configure test output
task karateReport(type: Copy) {
    dependsOn test
    from 'target/karate-reports'
    into 'build/karate-reports'
}

dockerCompose {
    useComposeFiles.set(['docker-compose.yml'])
    removeVolumes.set(true)
    removeOrphans.set(true)
    waitForTcpPorts.set(true)
    captureContainersOutput.set(true)
    stopContainers.set(true)
}

test.dependsOn composeUp
test.finalizedBy composeDown