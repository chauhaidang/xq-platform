plugins {
    id 'java'
    id 'com.avast.gradle.docker-compose' version '0.17.6'
}

group = 'xq'
version = '1.0.0'

ext {
    karateVersion = '1.5.1'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation "io.karatelabs:karate-junit5:${karateVersion}"
    testImplementation(project(":xq-api-test-plan"))
    testImplementation(project(":jvm-kit-common"))
}

sourceSets {
    test {
        resources {
            srcDir file('src/test/java')
            exclude '**/*.java'
        }
    }
}

test {
    useJUnitPlatform()
    systemProperty "karate.env", System.properties.getProperty("karate.env", "system")
    systemProperty "karate.options", "--tags @regression"
    filter {
        includeTestsMatching("e2e.TestRunner")
    }
    outputs.upToDateWhen { false }
}

tasks.register('testDebug', Test.class) {
    group = 'verification'
    useJUnitPlatform()
    systemProperty "karate.env", System.properties.getProperty("karate.env", "system")
    systemProperty "karate.options", "--tags @regression"
    filter {
        includeTestsMatching("e2e.TestRunner")
    }
    outputs.upToDateWhen { false }
}

dockerCompose {
    useComposeFiles.set(['docker-compose.yml'])
    removeVolumes.set(true)
    removeOrphans.set(true)
    waitForTcpPorts.set(true)
    captureContainersOutput.set(true)
    stopContainers.set(true)
}

test.dependsOn composeUp
test.finalizedBy composeDown